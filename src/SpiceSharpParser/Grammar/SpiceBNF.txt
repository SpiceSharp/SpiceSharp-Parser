<NETLIST> ::= <TITLE> <NEW-LINE> <STATEMENTS> <NETLIST-ENDING> 
		| <NEW-LINE> <STATEMENTS> <NETLIST-ENDING>
<NETLIST-WITHOUT-TITLE> ::= <STATEMENTS> <NETLIST-ENDING>
<NETLIST-ENDING> ::= ".END" <EOF> | ".END" <NEW-LINE> <EOF> | <EOF>
<STATEMENTS> ::= <STATEMENT> <STATEMENTS> | <NEW-LINE> <STATEMENTS> | <EMPTY> 
<STATEMENT> ::= <COMPONENT> <NEW-LINE> 
		| <SUBCKT> <NEW-LINE> 
		| <MODEL> <NEW-LINE> 
		| <CONTROL> <NEW-LINE> 
		| <COMMENT-LINE> <NEW-LINE> 
<COMPONENT> ::= <WORD> <PARAMETERS>
<COMMENT-LINE> ::= <COMMENT>
<SUBCKT> ::= ".SUBCKT" <WORD> <PARAMETERS> <NEW-LINE> <STATEMENTS> <SUBCKT-ENDING>
<SUBCKT-ENDING> ::= ".ENDS" <WORD> | ".ENDS"
<MODEL> ::= ".MODEL" <WORD> <PARAMETERS>
<CONTROL> ::= "." <WORD> <PARAMETERS>
			| ".ENDL" <PARAMETERS>
			| ".IF" (<EXPRESSION>)
			| ".ELSE" (<EXPRESSION>)
			| ".ELSEIF" (<EXPRESSION>)
			| ".ENDIF"
<PARAMETERS> ::= <PARAMETER> <PARAMETERS> | <EMPTY> 
<PARAMETER> ::= <VECTOR>
			| <PARAMETER-EQUAL>
			| <PARAMETER-BRACKET>
			| <PARAMETER-SINGLE> 
<PARAMETER-EQUAL> ::= <PARAMETER-EQUAL-SINGLE> 
				  | <WORD> "(" <PARAMETER-SINGLE> ")" "=" <PARAMETER-SINGLE>
   				  | <WORD> "(" <VECTOR> ")" "=" <PARAMETER-SINGLE>
<PARAMETER-EQUAL-SINGLE> :: = <WORD> "=" <PARAMETER-SINGLE>
<PARAMETER-BRACKET> ::= <WORD> "(" <PARAMETER-BRACKET-CONTENT> ")"
<PARAMETER-BRACKET-CONTENT> ::= <PARAMETERS>
<VECTOR> ::= <PARAMETER-SINGLE> "," <PARAMETER-SINGLE> <VECTOR-CONTINUE> 
<VECTOR-CONTINUE> ::= "," <PARAMETER-SINGLE> <VECTOR-CONTINUE> | <EMPTY>
<PARAMETER-SINGLE> ::= <WORD> | <VALUE> | <STRING> | <IDENTIFIER> | <REFERENCE> | <EXPRESSION> 

<EMPTY> ::= ""
<NEW-LINE> ::= "\r\n" | "\n" | "\r"
<EOF> ::= "$"